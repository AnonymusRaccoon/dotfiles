export LANG=en_US.UTF-8

export EDITOR=vim
export VISUAL=$EDITOR
alias edit=$EDITOR

export DOTFILES=$(realpath $(dirname $(realpath ~/.zshrc))/..)

[[ -f ~/.localrc ]] && source ~/.localrc

warn()
{
	printf "\r\033[00;31m$*\033[0m\n"
}

ZSH_THEME="powerlevel10k/powerlevel10k"
plugins=(zsh-syntax-highlighting sudo)

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
	source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

for file in $DOTFILES/**/*.zsh; do
	source $file
done

# Autocompletion selection (tab highlight + arrow move)
autoload -Uz compinit
compinit
zstyle ':completion:*' menu select

# ^Z when a job is suspended runs it in the background.
background()
{
	bg
}
zle -N background
bindkey ^Z background

if [[ -n $OMZ ]]; then
	if [[ -z $PL10K ]]; then
		local theme=${ZSH_CUSTOM:-$OMZ/custom}/themes/powerlevel10k
		[[ -d $theme ]] || warn "PowerLevel10K could not be found. Please install it to $theme or set it's path in the PL10K var."
	else
		local OMZ_THEME=${ZSH_CUSTOM:-$OMZ/custom}/themes
		if [[ ! -e $OMZ_THEME/powerlevel10k ]]; then
			mkdir -p $OMZ_THEME \
			  && ln -s $PL10K $OMZ_THEME/powerlevel10k \
			  || warn "Can't write to the OMZ directory. Maybe define a ZSH_CUSTOM var in the .localrc."
		fi
	fi
	source "$OMZ/oh-my-zsh.sh"
else
	warn "A .localrc file should exist on your home and contain the following variables declarations: OMZ"
fi